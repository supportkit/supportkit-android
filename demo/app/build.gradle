apply plugin: 'com.android.application'
apply plugin: "kotlin-android"

android {
    namespace 'io.smooch.demoapp'

    compileSdk 34

    defaultConfig {
        applicationId "io.smooch.demoapp"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
    }
    lintOptions {
        abortOnError false
    }
    flavorDimensions = ["dependencies"]
    productFlavors {
        local {
            dimension "dependencies"
        }
        remote {
            dimension "dependencies"
        }
    }
    if (rootProject.ext.skipRemoteFlavoredTasks) {
        println("Skipping remote flavored tasks...")
        variantFilter { variant ->
            if (variant.name.contains("remote") || variant.name.contains("Remote")) {
                variant.setIgnore(true)
                println("${variant.name} disabled.")
            }
        }
    }

}

dependencies {

    // Add Smooch ui to use the SDK out of the box
    localImplementation project(":ui")

    remoteImplementation(libs.smooch.ui) {
        changing = true
    }

    // Smooch core is included in ui and can be omitted
    // Add it to your dependencies if you intend to build a custom UI
    // implementation 'io.smooch:core:7.+'
    implementation libs.androidx.appcompat
    implementation libs.androidx.legacy.support.v4
}

apply plugin: 'com.google.gms.google-services'
